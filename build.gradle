plugins {
	id 'org.springframework.boot' version "${spring_boot_version}"
	id 'com.palantir.git-version' version '4.3.0'
}

version = gitVersion()

apply plugin: 'java'
apply plugin: 'checkstyle'

group = 'com.bannergress'

java {
  sourceCompatibility = JavaVersion.VERSION_17
}

repositories {
  maven {
    url = 'https://repo.osgeo.org/repository/geotools-releases/'
  }
	mavenCentral()
}

dependencies {
  implementation platform("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-restclient'
  implementation 'org.springframework.boot:spring-boot-starter-webclient'
  implementation 'org.springframework.boot:spring-boot-starter-flyway'
	implementation 'org.hibernate.orm:hibernate-envers'
  implementation 'org.hibernate.orm:hibernate-spatial'
  implementation platform('org.hibernate.search:hibernate-search-platform-bom:8.2.1.Final')
  implementation 'org.hibernate.search:hibernate-search-mapper-orm'
  implementation 'org.hibernate.search:hibernate-search-backend-lucene'
	implementation 'com.google.guava:guava:33.5.0-jre'
	implementation "com.google.maps:google-maps-services:${google_maps_services_version}"
  implementation "com.squareup.okhttp3:okhttp:${okhttp_version}"
  implementation 'tools.jackson.dataformat:jackson-dataformat-xml'
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1'
  implementation 'org.geotools:gt-referencing:34.2'

  annotationProcessor platform("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
  annotationProcessor 'org.hibernate.orm:hibernate-processor'
  annotationProcessor "net.karneim:pojobuilder:$pojobuilder_version"

  compileOnly "net.karneim:pojobuilder:$pojobuilder_version:annotations"

	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'org.flywaydb:flyway-database-postgresql'
  runtimeOnly 'org.apache.lucene:lucene-backward-codecs'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

checkstyle {
	configFile = file('config/checkstyle/simple-checkstyle.xml')
	sourceSets = [project.sourceSets.main, project.sourceSets.test]
	maxErrors = 0
	maxWarnings = 0
}

test {
	environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: 'dev'
	useJUnitPlatform()
}

bootRun {
	environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: 'dev'
	if (project.hasProperty('jvmArgs')) {
		jvmArgs = (project.jvmArgs.split('\\s+') as List)
	}
}

